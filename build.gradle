// Top-level build file where you can add configuration options common to all sub-projects/modules.

ext {
    projectVersion = '0.0.0.8'
    libraryName = 'geofence-manager'
    projectVersionCode = 1;
}

apply plugin: 'com.android.library'
apply plugin: 'android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.github.dcendents.android-maven'

apply from: new File('bintray.gradle')

android {
    compileSdkVersion 25
    buildToolsVersion "24.0.3"
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 25
        versionCode projectVersionCode
        versionName projectVersion
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

// Used by dcendents.android-maven to set the artifact id for maven
group = 'com.hgyllensvard'
version = projectVersion
project.archivesBaseName = libraryName


def googleServicesVersion = '9.6.1'

dependencies {

    // Play Services
    compile "com.google.android.gms:play-services-maps:$googleServicesVersion"

    // Google support libraries
    compile 'com.android.support:appcompat-v7:25.0.1'

    // Dependency Injection
    compile 'com.google.dagger:dagger:2.7'
    apt 'com.google.dagger:dagger-compiler:2.7'

    // Reactive programming
    compile 'io.reactivex.rxjava2:rxjava:2.0.1'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'com.tbruyelle.rxpermissions2:rxpermissions:0.8.2@aar'

    // Sending arguments to fragments
    compile 'com.hannesdorfmann.fragmentargs:annotation:3.0.2'
    apt 'com.hannesdorfmann.fragmentargs:processor:3.0.2'

    // Logging helper
    compile 'com.jakewharton.timber:timber:4.3.1'

    testCompile 'junit:junit:4.12'

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}


buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.2'

        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'

        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'

        classpath "com.neenbedankt.gradle.plugins:android-apt:1.8"

        classpath "me.tatarka:gradle-retrolambda:3.4.0"
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
